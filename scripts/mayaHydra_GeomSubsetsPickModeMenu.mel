// addMenuCallback and removeMenuCallback copied from MayaUSD
// See here : [TODO : insert link]

// safely add a post menu callback to a menu
proc addMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // append the callback
    menu -e -pmc ($existingCallbacks + ";" + $cmd + ";") $menuName;
}

// safely remove a post menu callback to a menu
proc removeMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // remove our callback from the string of callbacks
    string $newCallbacks =
        `substitute (";"+$cmd+".*;") $existingCallbacks ""`;
    menu -e -pmc $newCallbacks $menuName;
}

global proc mayaHydra_GeomSubsetsPickModeMenu_Setup() {
    mayaHydra_createRuntimeCommands;
    global string $gMainSelectMenu; // Maya's "Select" menu
    addMenuCallback($gMainSelectMenu, "mayaHydra_GeomSubsetsPickModeMenu_SelectMenuOpenedCallback");
}

global proc mayaHydra_GeomSubsetsPickModeMenu_SelectMenuOpenedCallback() {
    if (!`pluginInfo -q -loaded mayaUsdPlugin`) {
        return;
    }

    global string $gMainSelectMenu; // Maya's "Select" menu
    removeMenuCallback($gMainSelectMenu, "mayaHydra_GeomSubsetsPickModeMenu_SelectMenuOpenedCallback");

    scriptJob -idleEvent "mayaHydra_GeomSubsetsPickModeMenu_CreateMenuItems" -runOnce true;
}

global proc mayaHydra_GeomSubsetsPickModeMenu_CreateMenuItems() {
    global string $gMayaUsdSelectModeSubMenu;
    if ($gMayaUsdSelectModeSubMenu == "" || !`menuItem -q -exists $gMayaUsdSelectModeSubMenu`) {
        print "Error : USD selection submenu was not found, could not add GeomSubsets pick mode UI.\n";
        return;
    }

    $origParent = `setParent -q -menu`;
    setParent -menu $gMayaUsdSelectModeSubMenu;

    menuItem -divider true -dividerLabel "GeomSubsets (Hydra only)";

    radioMenuItemCollection;
    menuItem -radioButton on -runTimeCommand ("mayaHydraGeomSubsetsPickMode_Prims") geomSubsetsPickMode_Prims;
    menuItem -radioButton off -runTimeCommand ("mayaHydraGeomSubsetsPickMode_GeomSubsets") geomSubsetsPickMode_GeomSubsets;

    mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons();

    scriptJob -parent $gMayaUsdSelectModeSubMenu
            -optionVarChanged "mayaHydra_GeomSubsetsPickMode" "mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons";

    setParent -menu $origParent;
}


// Update the menu item radio buttons in the GeomSubsets pick mode UI
global proc mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons() {
    // Note: we do all string compares as case-insensitive (lowercase)
    //       as the menu labels are capitalized.
    string $pickMode = `tolower(mayaHydra_getGeomSubsetsPickMode())`;
    switch($pickMode) {
        case "":
            menuItem -e -radioButton on geomSubsetsPickMode_Prims;
            break;
        default:
            // Enable the menu item based on the label.
            global string $gMayaUsdSelectModeSubMenu;
            $menuItems = `menu -q -itemArray $gMayaUsdSelectModeSubMenu`;
            for($i = 0; $i < size($menuItems); $i++) {
                string $label = `menuItem -q -label $menuItems[$i]`;
                if(`tolower($label)` == $pickMode) {
                    menuItem -e -radioButton on $menuItems[$i];
                    break;
                }
            }
    }
}


///////////////////////////////////////////////////////////////////////////////
// mayaHydra_setGeomSubsetsPickMode
// Helper function to set the GeomSubsets pick mode.
global proc mayaHydra_setGeomSubsetsPickMode(string $mode) {
    optionVar -stringValue "mayaHydra_GeomSubsetsPickMode" $mode;
}

///////////////////////////////////////////////////////////////////////////////
// mayaHydra_getGeomSubsetsPickMode
// Helper function to get the GeomSubsets pick mode.
global proc string mayaHydra_getGeomSubsetsPickMode() {
    string $mode = "";
    if (`optionVar -exists "mayaHydra_GeomSubsetsPickMode"`) {
        $mode = `optionVar -q "mayaHydra_GeomSubsetsPickMode"`;
    }
    return $mode;
}

global proc mayaHydra_createRuntimeCommands() {
    if (!`runTimeCommand -exists mayaHydraGeomSubsetsPickMode_Prims`) {
        runTimeCommand -default true -plugin "mayaHydra"
            -label      "Prims"
            -annotation "Select whole prims"
            -category   "Menu items.Maya USD"
            -command    ("mayaHydra_setGeomSubsetsPickMode(\"Prims\")")
        mayaHydraGeomSubsetsPickMode_Prims;
    }
    if (!`runTimeCommand -exists mayaHydraGeomSubsetsPickMode_GeomSubsets`) {
        runTimeCommand -default true -plugin "mayaHydra"
            -label      "GeomSubsets"
            -annotation "Select GeomSubsets"
            -category   "Menu items.Maya USD"
            -command    ("mayaHydra_setGeomSubsetsPickMode(\"GeomSubsets\")")
        mayaHydraGeomSubsetsPickMode_GeomSubsets;
    }
}
