// Contents of this file are largely adaptations of MayaUSD code for Kind and Point Instance picking modes UI setup.
// See the following MEL files in MayaUSD : [TODO : list file names, with links?]

// addMenuCallback and removeMenuCallback were copied over from MayaUSD
// See here : [TODO : insert link]

// safely add a post menu callback to a menu
proc addMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // append the callback
    menu -e -pmc ($existingCallbacks + ";" + $cmd + ";") $menuName;
}

// safely remove a post menu callback to a menu
proc removeMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // remove our callback from the string of callbacks
    string $newCallbacks =
        `substitute (";"+$cmd+".*;") $existingCallbacks ""`;
    menu -e -pmc $newCallbacks $menuName;
}

global string $gMayaHydra_GeomSubsetsPickMode_Divider;
global string $gMayaHydra_GeomSubsetsPickMode_RadioButtonsGroup;

// Helper function to get the GeomSubsets pick mode.
global proc string mayaHydra_GeomSubsetsPickMode_GetMode() {
    string $mode = "";
    if (`optionVar -exists "mayaHydra_GeomSubsetsPickMode"`) {
        $mode = `optionVar -q "mayaHydra_GeomSubsetsPickMode"`;
    }
    return $mode;
}

// Helper function to set the GeomSubsets pick mode.
global proc mayaHydra_GeomSubsetsPickMode_SetMode(string $mode) {
    optionVar -stringValue "mayaHydra_GeomSubsetsPickMode" $mode;
}

global proc mayaHydra_GeomSubsetsPickMode_SetupRuntimeCommands() {
    if (!`runTimeCommand -exists mayaHydra_GeomSubsetsPickMode_SetPrimsAndInstances`) {
        runTimeCommand -default true -plugin "mayaHydra"
            -label      "Prims and Instances"
            -annotation "Select whole prims"
            -category   "Menu items.Maya USD"
            -command    ("mayaHydra_GeomSubsetsPickMode_SetMode(\"PrimsAndInstances\")")
        mayaHydra_GeomSubsetsPickMode_SetPrimsAndInstances;
    }
    if (!`runTimeCommand -exists mayaHydra_GeomSubsetsPickMode_SetGeomSubsets`) {
        runTimeCommand -default true -plugin "mayaHydra"
            -label      "GeomSubsets"
            -annotation "Select GeomSubsets"
            -category   "Menu items.Maya USD"
            -command    ("mayaHydra_GeomSubsetsPickMode_SetMode(\"GeomSubsets\")")
        mayaHydra_GeomSubsetsPickMode_SetGeomSubsets;
    }
}

global proc mayaHydra_GeomSubsetsPickMode_SetupUI() {
    mayaHydra_GeomSubsetsPickMode_SetupRuntimeCommands;
    global string $gMainSelectMenu; // Maya's "Select" menu
    addMenuCallback($gMainSelectMenu, "mayaHydra_GeomSubsetsPickModeMenu_SelectMenuOpenedCallback");
}

global proc mayaHydra_GeomSubsetsPickMode_TeardownUI() {
    global string $gMayaHydra_GeomSubsetsPickMode_Divider;
    global string $gMayaHydra_GeomSubsetsPickMode_RadioButtonsGroup;
    deleteUI -menuItem 
        $gMayaHydra_GeomSubsetsPickMode_Divider 
        $gMayaHydra_GeomSubsetsPickMode_RadioButtonsGroup;
    $gMayaHydra_GeomSubsetsPickMode_Divider = "";
    $gMayaHydra_GeomSubsetsPickMode_RadioButtonsGroup = "";
}

global proc mayaHydra_GeomSubsetsPickModeMenu_SelectMenuOpenedCallback() {
    // Explanation of how we augment MayaUSD's selection modes menu with our custom options
    // for GeomSubsets picking :
    // To create its selection modes menu, MayaUSD, on plugin UI load, will setup a callback
    // on when the "Select" menu in Maya is opened. The callback will then create the selection
    // modes submenu and its menu items. So what we do in order to add our own menu items is that
    // we also add our own callback on the "Select" menu. However, we can't count on the order
    // in which callbacks will be registered and processed, and just blindly add the menu items here.
    // To work around this, we thus create the UI elements in a method that will be called by an idle 
    // event/scriptJob, which will be executed after the "Select" menu open callbacks have been 
    // processed. The USD selection modes menu should then exist, and we can add our menu items then.

    // If MayaUSD is not loaded, the USD selection modes menu won't exist, so no need to try and add
    // our own menu items.
    if (!`pluginInfo -q -loaded mayaUsdPlugin`) {
        return;
    }

    // If our custom UI elements name is empty, then they do not exist or have been torn down previously,
    // so create them. Note that we check that all UI elements have empty names : normally they should
    // always either all have a name and exist, or all have no name and not exist.
    global string $gMayaHydra_GeomSubsetsPickMode_Divider;
    global string $gMayaHydra_GeomSubsetsPickMode_RadioButtonsGroup;
    if ($gMayaHydra_GeomSubsetsPickMode_Divider == ""
        && $gMayaHydra_GeomSubsetsPickMode_RadioButtonsGroup == "") {
        scriptJob -idleEvent "mayaHydra_GeomSubsetsPickModeMenu_CreateMenuItems" -runOnce true;
    }
}

global proc mayaHydra_GeomSubsetsPickModeMenu_CreateMenuItems() {
    global string $gMayaUsdSelectModeSubMenu;
    if ($gMayaUsdSelectModeSubMenu == "" || !`menuItem -q -exists $gMayaUsdSelectModeSubMenu`) {
        print "Error : USD selection submenu was not found, could not add GeomSubsets pick mode UI.\n";
        return;
    }

    $origParent = `setParent -q -menu`;
    setParent -menu $gMayaUsdSelectModeSubMenu;

    $gMayaHydra_GeomSubsetsPickMode_Divider = `menuItem -divider true -dividerLabel "GeomSubsets (Hydra only)"`;

    $gMayaHydra_GeomSubsetsPickMode_RadioButtonsGroup = `radioMenuItemCollection`;
    menuItem -radioButton on -runTimeCommand ("mayaHydra_GeomSubsetsPickMode_SetPrimsAndInstances") mayaHydra_GeomSubsetsPickMode_PrimsAndInstances;
    menuItem -radioButton off -runTimeCommand ("mayaHydra_GeomSubsetsPickMode_SetGeomSubsets") mayaHydra_GeomSubsetsPickMode_GeomSubsets;

    mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons();

    scriptJob -parent $gMayaUsdSelectModeSubMenu
            -optionVarChanged "mayaHydra_GeomSubsetsPickMode" "mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons";

    setParent -menu $origParent;
}

// Update the menu item radio buttons in the GeomSubsets pick mode UI
global proc mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons() {
    // Note: we do all string compares as case-insensitive (lowercase)
    //       as the menu labels are capitalized.
    string $pickMode = `tolower(mayaHydra_GeomSubsetsPickMode_GetMode())`;
    switch($pickMode) {
        case "":
            menuItem -e -radioButton on mayaHydra_GeomSubsetsPickMode_PrimsAndInstances;
            break;
        default:
            // Enable the menu item based on the label.
            global string $gMayaUsdSelectModeSubMenu;
            $menuItems = `menu -q -itemArray $gMayaUsdSelectModeSubMenu`;
            for($i = 0; $i < size($menuItems); $i++) {
                string $label = `menuItem -q -label $menuItems[$i]`;
                if(`tolower($label)` == $pickMode) {
                    menuItem -e -radioButton on $menuItems[$i];
                    break;
                }
            }
    }
}
