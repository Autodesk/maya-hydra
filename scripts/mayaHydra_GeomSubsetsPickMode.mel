// Contents of this file are largely adaptations of MayaUSD code for Kind and Point Instance picking modes UI setup.
// See the following MEL files in MayaUSD : [TODO : list file names, with links?]

// addMenuCallback and removeMenuCallback were copied over from MayaUSD
// See here : [TODO : insert link]

// safely add a post menu callback to a menu
proc addMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // append the callback
    menu -e -pmc ($existingCallbacks + ";" + $cmd + ";") $menuName;
}

// safely remove a post menu callback to a menu
proc removeMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // remove our callback from the string of callbacks
    string $newCallbacks =
        `substitute (";"+$cmd+".*;") $existingCallbacks ""`;
    menu -e -pmc $newCallbacks $menuName;
}

global string $gMayaHydra_GeomSubsetsPickMode_Divider;
global string $gMayaHydra_GeomSubsetsPickMode_ButtonPrims;
global string $gMayaHydra_GeomSubsetsPickMode_ButtonGeomSubsets;

// Helper function to get the GeomSubsets pick mode.
global proc string mayaHydra_GeomSubsetsPickMode_GetMode() {
    string $mode = "";
    if (`optionVar -exists "mayaHydra_GeomSubsetsPickMode"`) {
        $mode = `optionVar -q "mayaHydra_GeomSubsetsPickMode"`;
    }
    return $mode;
}

// Helper function to set the GeomSubsets pick mode.
global proc mayaHydra_GeomSubsetsPickMode_SetMode(string $mode) {
    optionVar -stringValue "mayaHydra_GeomSubsetsPickMode" $mode;
}

global proc mayaHydra_GeomSubsetsPickMode_SetupRuntimeCommands() {
    if (!`runTimeCommand -exists mayaHydra_GeomSubsetsPickMode_SetPrims`) {
        runTimeCommand -default true -plugin "mayaHydra"
            -label      "Prims"
            -annotation "Select whole prims"
            -category   "Menu items.Maya USD"
            -command    ("mayaHydra_GeomSubsetsPickMode_SetMode(\"Prims\")")
        mayaHydra_GeomSubsetsPickMode_SetPrims;
    }
    if (!`runTimeCommand -exists mayaHydra_GeomSubsetsPickMode_SetGeomSubsets`) {
        runTimeCommand -default true -plugin "mayaHydra"
            -label      "GeomSubsets"
            -annotation "Select GeomSubsets"
            -category   "Menu items.Maya USD"
            -command    ("mayaHydra_GeomSubsetsPickMode_SetMode(\"GeomSubsets\")")
        mayaHydra_GeomSubsetsPickMode_SetGeomSubsets;
    }
}

global proc mayaHydra_GeomSubsetsPickMode_SetupUI() {
    mayaHydra_GeomSubsetsPickMode_SetupRuntimeCommands;
    global string $gMainSelectMenu; // Maya's "Select" menu
    addMenuCallback($gMainSelectMenu, "mayaHydra_GeomSubsetsPickModeMenu_SelectMenuOpenedCallback");
}

global proc mayaHydra_GeomSubsetsPickMode_TeardownUI() {
    global string $gMayaHydra_GeomSubsetsPickMode_Divider;
    global string $gMayaHydra_GeomSubsetsPickMode_ButtonPrims;
    global string $gMayaHydra_GeomSubsetsPickMode_ButtonGeomSubsets;
    deleteUI -menuItem 
        $gMayaHydra_GeomSubsetsPickMode_Divider 
        $gMayaHydra_GeomSubsetsPickMode_ButtonPrims 
        $gMayaHydra_GeomSubsetsPickMode_ButtonGeomSubsets;
    $gMayaHydra_GeomSubsetsPickMode_Divider = "";
    $gMayaHydra_GeomSubsetsPickMode_ButtonPrims = "";
    $gMayaHydra_GeomSubsetsPickMode_ButtonGeomSubsets = "";
}

global proc mayaHydra_GeomSubsetsPickModeMenu_SelectMenuOpenedCallback() {
    if (!`pluginInfo -q -loaded mayaUsdPlugin`) {
        return;
    }

    global string $gMayaHydra_GeomSubsetsPickMode_Divider;
    global string $gMayaHydra_GeomSubsetsPickMode_ButtonPrims;
    global string $gMayaHydra_GeomSubsetsPickMode_ButtonGeomSubsets;
    if ($gMayaHydra_GeomSubsetsPickMode_Divider == ""
        && $gMayaHydra_GeomSubsetsPickMode_ButtonPrims == ""
        && $gMayaHydra_GeomSubsetsPickMode_ButtonGeomSubsets == "") {
        scriptJob -idleEvent "mayaHydra_GeomSubsetsPickModeMenu_CreateMenuItems" -runOnce true;
    }
}

global proc mayaHydra_GeomSubsetsPickModeMenu_CreateMenuItems() {
    global string $gMayaUsdSelectModeSubMenu;
    if ($gMayaUsdSelectModeSubMenu == "" || !`menuItem -q -exists $gMayaUsdSelectModeSubMenu`) {
        print "Error : USD selection submenu was not found, could not add GeomSubsets pick mode UI.\n";
        return;
    }

    $origParent = `setParent -q -menu`;
    setParent -menu $gMayaUsdSelectModeSubMenu;

    $gMayaHydra_GeomSubsetsPickMode_Divider = `menuItem -divider true -dividerLabel "GeomSubsets (Hydra only)"`;

    radioMenuItemCollection;
    $gMayaHydra_GeomSubsetsPickMode_ButtonPrims = `menuItem -radioButton on -runTimeCommand ("mayaHydra_GeomSubsetsPickMode_SetPrims") mayaHydra_GeomSubsetsPickMode_Prims`;
    $gMayaHydra_GeomSubsetsPickMode_ButtonGeomSubsets = `menuItem -radioButton off -runTimeCommand ("mayaHydra_GeomSubsetsPickMode_SetGeomSubsets") mayaHydra_GeomSubsetsPickMode_GeomSubsets`;

    mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons();

    scriptJob -parent $gMayaUsdSelectModeSubMenu
            -optionVarChanged "mayaHydra_GeomSubsetsPickMode" "mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons";

    setParent -menu $origParent;
}

// Update the menu item radio buttons in the GeomSubsets pick mode UI
global proc mayaHydra_GeomSubsetsPickModeMenu_UpdateRadioButtons() {
    // Note: we do all string compares as case-insensitive (lowercase)
    //       as the menu labels are capitalized.
    string $pickMode = `tolower(mayaHydra_GeomSubsetsPickMode_GetMode())`;
    switch($pickMode) {
        case "":
            menuItem -e -radioButton on mayaHydra_GeomSubsetsPickMode_Prims;
            break;
        default:
            // Enable the menu item based on the label.
            global string $gMayaUsdSelectModeSubMenu;
            $menuItems = `menu -q -itemArray $gMayaUsdSelectModeSubMenu`;
            for($i = 0; $i < size($menuItems); $i++) {
                string $label = `menuItem -q -label $menuItems[$i]`;
                if(`tolower($label)` == $pickMode) {
                    menuItem -e -radioButton on $menuItems[$i];
                    break;
                }
            }
    }
}
