// addMenuCallback and removeMenuCallback copied from MayaUSD
// See here : [TODO : insert link]

///////////////////////////////////////////////////////////////////////////////
// addMenuCallback
// safely add a post menu callback to a menu
proc addMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // append the callback
    menu -e -pmc ($existingCallbacks + ";" + $cmd + ";") $menuName;
}

///////////////////////////////////////////////////////////////////////////////
// removeMenuCallback
// safely remove a post menu callback to a menu
proc removeMenuCallback(string $menuName, string $cmd) {
    string $existingCallbacks = `menu -q -pmc $menuName`;
    // remove our callback from the string of callbacks
    string $newCallbacks =
        `substitute (";"+$cmd+".*;") $existingCallbacks ""`;
    menu -e -pmc $newCallbacks $menuName;
}

///////////////////////////////////////////////////////////////////////////////
// addMenuItemCallback
// safely add a post menu item callback to a menu item
proc addMenuItemCallback(string $menuItemName, string $cmd) {
    string $existingCallbacks = `menuItem -q -pmc $menuItemName`;
    // append the callback
    menuItem -e -pmc ($existingCallbacks + ";" + $cmd + ";") $menuItemName;
}

///////////////////////////////////////////////////////////////////////////////
// removeMenuItemCallback
// safely remove a post menu item callback to a menu item
proc removeMenuItemCallback(string $menuItemName, string $cmd) {
    string $existingCallbacks = `menuItem -q -pmc $menuItemName`;
    // remove our callback from the string of callbacks
    string $newCallbacks =
        `substitute (";"+$cmd+".*;") $existingCallbacks ""`;
    menuItem -e -pmc $newCallbacks $menuItemName;
}


global proc mayaHydraMenus_PrepareMenus() {
    global string $gMainSelectMenu; // Maya's "Select" menu
    addMenuCallback($gMainSelectMenu, "mayaHydraMenus_SelectMenuOpenedCallback");
}

global int $g_mayaHydraMenus_SelectMenuIdleEventId;

global string $gMayaUsdSelectModeSubMenu;

global int $g_mayaHydraMenus_PickModeMenuEventRegistered;

global proc mayaHydraMenus_SelectMenuOpenedCallback() {
    global string $gMainSelectMenu; // Maya's "Select" menu
    removeMenuCallback($gMainSelectMenu, "mayaHydraMenus_SelectMenuOpenedCallback");

    global int $g_mayaHydraMenus_SelectMenuIdleEventId;
    $g_mayaHydraMenus_SelectMenuIdleEventId = `scriptJob -idleEvent "mayaHydraMenus_CreateSelectMenuItems"`;
}

global proc mayaHydraMenus_CreateSelectMenuItems() {
    global string $gMayaUsdSelectModeSubMenu;
    // Check if the USD Selection menu exists, and if not, wait for it by returning immediately
    if ($gMayaUsdSelectModeSubMenu == "" || !`menuItem -q -exists $gMayaUsdSelectModeSubMenu`) {
        return;
    }

    print "ScriptJob running";

    global int $g_mayaHydraMenus_PickModeMenuEventRegistered;
    if ($g_mayaHydraMenus_PickModeMenuEventRegistered == 0) {
        addMenuItemCallback($gMayaUsdSelectModeSubMenu, "mayaHydraMenus_CreatePickModeMenuItems");
        $g_mayaHydraMenus_PickModeMenuEventRegistered = 1;
    }
}

global proc mayaHydraMenus_CreatePickModeMenuItems() {
    global string $gMayaUsdSelectModeSubMenu;
    removeMenuItemCallback($gMayaUsdSelectModeSubMenu, "mayaHydraMenus_CreatePickModeMenuItems");

    global int $g_mayaHydraMenus_SelectMenuIdleEventId;
    scriptJob -kill $g_mayaHydraMenus_SelectMenuIdleEventId -force;

    createPickModeMenuItems();
}

global proc createPickModeMenuItems() {
    $origParent = `setParent -q -menu`;
    global string $gMayaUsdSelectModeSubMenu;
    setParent -menu $gMayaUsdSelectModeSubMenu;

    menuItem -divider true -dividerLabel "GeomSubsets (Hydra viewports only)";

    radioMenuItemCollection;
    menuItem -radioButton on -runTimeCommand ("mayaHydraGeomSubsetsPickMode_Prims") geomSubsetsPickMode_Prims;
    menuItem -radioButton off -runTimeCommand ("mayaHydraGeomSubsetsPickMode_GeomSubsets") geomSubsetsPickMode_GeomSubsets;

    mayaHydra_updateGeomSubsetsPickModeMenuItems();

    scriptJob -parent $gMayaUsdSelectModeSubMenu
            -optionVarChanged "mayaHydra_GeomSubsetsPickMode" "mayaHydra_updateGeomSubsetsPickModeMenuItems";

    setParent -menu $origParent;
}


///////////////////////////////////////////////////////////////////////////////
// mayaHydra_updateGeomSubsetsPickModeMenuItems
// Update the menu item radio buttons in the GeomSubsets Pick Mode menu
global proc mayaHydra_updateGeomSubsetsPickModeMenuItems() {
    // Note: we do all string compares as case-insenstive (lowercase)
    //       as the menu labels are capitalized.
    string $pickMode = `tolower(mayaHydra_getGeomSubsetsPickMode())`;
    switch($pickMode) {
        case "":
            menuItem -e -radioButton on geomSubsetsPickMode_Prims;
            break;
        default:
            // Enable the menu item based on the label.
            global string $gMayaUsdSelectModeSubMenu;
            $menuItems = `menu -q -itemArray $gMayaUsdSelectModeSubMenu`;
            for($i = 0; $i < size($menuItems); $i++) {
                string $label = `menuItem -q -label $menuItems[$i]`;
                if(`tolower($label)` == $pickMode) {
                    menuItem -e -radioButton on $menuItems[$i];
                    break;
                }
            }
    }
}